(cl:in-package #:sicl-sequence)

(defmethod map-into ((list list) function &rest sequences)
  (let ((function (function-designator-function function))
        (rest list))
    (flet ((fn (&rest arguments)
             (setf (car rest)
                   (apply function arguments))
             (pop rest)))
      (declare (dynamic-extent #'fn))
      (apply #'map-for-effect #'fn sequences)
      list)))

(replicate-for-each-vector-class #1=#:vector-class
  (defmethod map-into ((vector #1#) function &rest sequences)
    (when (adjustable-array-p vector)
      (setf (fill-pointer vector)
            (array-total-size vector)))
    (let ((function (function-designator-function function))
          (index 0))
      (declare (array-length index))
      (flet ((fn (&rest arguments)
               (setf (elt vector index)
                     (funcall function arguments))
               (incf index)))
        (declare (dynamic-extent #'fn))
        (apply #'map-for-effect #'fn sequences))
      (when (adjustable-array-p vector)
        (setf (fill-pointer vector) index))
      vector)))
